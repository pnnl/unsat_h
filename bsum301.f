      PROGRAM BSUM
!-----------------------------------------------------------------------
!     Uses READREC read structure from UNSAT-H 
!     Uses SUMMARY read structure from UNSAT-H
!     Uses UNSATH.INC with no modifications
!
!     Processes binary output files (*.res) generated by UNSAT-H to
!       generate a file containing summary water balance data from 
!       every file.
!-----------------------------------------------------------------------
      INCLUDE 'unsath.inc'
      PARAMETER (MAXNF = 100)
!     If you change, MAXNF, be sure to reset maxnf in the data stmt.
      CHARACTER*80 FILES(MAXNF),FILDES,EXT*4
      INTEGER F_OPT
      data BSV/3.01/
      EXT = '.res'
      WRITE(LUS,9000)
      WRITE(LUS,6000) BSV
50    NF = 1
      SUM_TRAIN  = 0
      SUM_TPET   = 0
      SUM_TTRA   = 0
      SUM_TE     = 0
      SUM_TRUNOF = 0
      SUM_TQL    = 0
      SUM_TERR   = 0
      WRITE(LUS,6010)
      READ(LUR,*) F_OPT
      IF (F_OPT .EQ. 0) THEN
        WRITE(LUS,6020)
        READ(LUR,5000) FILDES
10      WRITE(LUS,6021) MAXNF
6021  FORMAT(/,                                                         &
     &' The ending year must not be less than the starting year and',/, &
     &' the total number of years must not exceed the dimension',I4,//, &
     &' Enter the starting and ending years ==> ',$)
        READ(LUR,*) IYRBEG,IYREND
        IF (IYREND .LT. IYRBEG) THEN
          WRITE(LUS,6022) IYRBEG,IYREND
6022  FORMAT(/,                                                         &
     &' The ending year',I5,' exceeds the starting year',I5,/,          &
     &'  You must enter new years.')
          GO TO 10
        ENDIF
        NF = IYREND-IYRBEG+1
        IF (NF .GT. MAXNF) THEN
          WRITE(LUS,6023) IYRBEG,IYREND,NF,MAXNF
6023  FORMAT(/,                                                         &
     &' The starting year',I5,' and ending year',I5,' yield',/,         &
     &I10,' separate files.  This code is only dimensioned for',/,      &
     &I10,' separate files.  You must enter new years.')
          GO TO 10
        ENDIF
        NCHR = INDEX(FILDES,' ')
        IYEAR = IYRBEG
        DO I=1,NF
          FILES(I) = FILDES
          WRITE(FILES(I)(NCHR:NCHR+3),'(I4)') IYEAR
          WRITE(FILES(I)(NCHR+4:NCHR+7),'(A4)') EXT
          IYEAR = IYEAR+1
        ENDDO
        IYEAR = IYRBEG
      ELSE IF (F_OPT .EQ. 1) THEN
        WRITE(LUS,6030) MAXNF
        READ(LUR,*) NF
        IF (NF .GT. MAXNF) then
          NF = MAXNF
          write(*,*) ' nf exceeded maxnf; nf reset to maxnf = ',MAXNF
        endif
        WRITE(LUS,6035)
        DO I=1,NF
          WRITE(LUS,6040) I
          READ(LUR,5000) FILES(I)
        ENDDO
35      WRITE(LUS,*) ' The entered file names are:'
        WRITE(LUS,6050) (FILES(I),I=1,NF)
        WRITE(LUS,6060)
        READ(LUR,*) IREENT
        IF (IREENT .EQ. 1) GO TO 35
        WRITE(LUS,*) ' Enter the year of FILE(1), e.g. 1987'
        READ(LUR,*) IYEAR
      ELSE
        GO TO 999
      ENDIF
!
!     Choose output option (screen or file)
!
      LOUT = LUS
      WRITE(LUS,6070)
      READ(LUR,*) OUT_OPT
      IF (OUT_OPT .EQ. 1) THEN
        LOUT = LUW
        OFILE = 'bsum301.out'
        OPEN (UNIT=LOUT,FILE=OFILE,STATUS='UNKNOWN')
        CLOSE(UNIT=LOUT,STATUS='DELETE')
        OPEN (UNIT=LOUT,FILE=OFILE,STATUS='NEW')
      ENDIF
      WRITE(LOUT,6090) BSV,FILES(1)
      WRITE(LOUT,6095)
!
!     Read each file to verify correctness
!
      DO L=1,NF
        OPEN(UNIT=LUI,FILE=files(L),STATUS='OLD',FORM='UNFORMATTED',    &
     &     ACCESS='DIRECT',RECL=100,IOSTAT=IOS)
        IF (IOS .GT. 0) THEN
!
!     File not found
!
          WRITE(LUS,6150) TMFILE
          STOP
        ENDIF
        READ(LUI,REC=1) NPT,IPLANT,IHEAT
        CLOSE(LUI)
        IRL = IRLMOD*MAX(110,(9*IHEAT+IPLANT+5)*NPT+45)
        OPEN(UNIT=LUI,FILE=files(L),STATUS='OLD',FORM='UNFORMATTED',    &
     &     ACCESS='DIRECT',RECL=IRL)
        INQUIRE(UNIT=LUI,NAME=RFILE)
        READ(LUI,REC=1) NPT,IPLANT,IHEAT,(Z(I),I=1,NPT),UV,STOPHR,TSOIL,&
     &  IFILE,SDATE,STIME,TITLE,IDEND,NPRINT,IEVOPT,NTOTAL,IDBEG,IVAPOR,&
     &  ICLOUD,IETOPT,ISHOPT,UPPER,UPPERH,LOWER,LOWERH,IYEAR
        IF (BSV .NE. UV) THEN
          WRITE(6,45) DOV,UV
          STOP
        ENDIF
        CLOSE(UNIT=LUI)
      ENDDO
45    FORMAT(' ERROR:  BSUM Version No. ',F4.2,' does not match',/,     &
     &       '         UNSAT-H Version No. ',F4.2,'.  Program stopped.')
!
!     Read data from *.res files and send to screen or output file
!
      DO L=1,NF
        OPEN(UNIT=LUI,FILE=files(L),STATUS='OLD',FORM='UNFORMATTED',    &
     &    ACCESS='DIRECT',RECL=100)
        READ(LUI,REC=1) NPT,IPLANT,IHEAT
        CLOSE(LUI)
        IRL = IRLMOD*MAX(110,(9*IHEAT+IPLANT+5)*NPT+45)
        OPEN(UNIT=LUI,FILE=files(L),STATUS='OLD',FORM='UNFORMATTED',    &
     &    ACCESS='DIRECT',RECL=IRL)
        INQUIRE(UNIT=LUI,NAME=RFILE)
        READ(LUI,REC=1) NPT,IPLANT,IHEAT,(Z(I),I=1,NPT),UV,STOPHR,TSOIL,&
     &  IFILE,SDATE,STIME,TITLE,IDEND,NPRINT,IEVOPT,NTOTAL,IDBEG,IVAPOR,&
     &  ICLOUD,IETOPT,ISHOPT,UPPER,UPPERH,LOWER,LOWERH,IYEAR
        MAXREC = 3+(IDEND-IDBEG+1)*(1+NTOTAL*NPRINT)-                   &
     &         NINT(NPRINT*NTOTAL*(1-STOPHR/24.))
        DO I=1,MAXREC
          READ(LUI,REC=I,ERR=48)
        ENDDO
48      IF (I-1 .NE. MAXREC) WRITE(LUS,6110) I-1,MAXREC-I+1
        IREC = 2
!
!     Read the initial conditions to get initial storage
!     Format is as used in READREC, subroutine to UNSAT-H Version 3.00
!
      IF (IPLANT .EQ. 0) THEN
        IF (IHEAT .EQ. 0) THEN
          READ(LUI,REC=IREC,ERR=200) IDAY,HOUR,(H(J),THETA(J),DAYQL(J), &
     &      DAYQVH(J),J=1,NPT),DAYINF,DAYRAN,DAYE,DAYTRA,DAYRUN,        &
     &      TPREV,TMOIST,DAYSTP,PE,PT,DAYTIM,DAYAST,DAYUBC,RHMEAN,      &
     &      TMEAN,HDRY,DAYRN,DAYSHF,DAYSEN,DHPREV,HSTORE,DAYQHW0,       &
     &      TA,TMAX,TMIN,VD_A,WIND,CLOUD,SR_MEAS,DAYLE,DAYSDH,DAYHBE
        ELSE
          READ(LUI,REC=IREC,ERR=200) IDAY,HOUR,(H(J),THETA(J),DAYQL(J), &
     &      DAYQVH(J),DAYQVT(J),T(J),J=1,NPT),                          &
     &      DAYINF,DAYRAN,DAYE,DAYTRA,DAYRUN,                           &
     &      TPREV,TMOIST,DAYSTP,PE,PT,DAYTIM,DAYAST,DAYUBC,RHMEAN,      &
     &      TMEAN,HDRY,DAYRN,DAYSHF,DAYSEN,DHPREV,HSTORE,DAYQHW0,       &
     &      TA,TMAX,TMIN,VD_A,WIND,CLOUD,SR_MEAS,DAYLE,DAYSDH,DAYHBE,   &
     &      (DAYQHC(J),DAYQHW(J),DAYQHV(J),J=1,NPT)
        ENDIF
      ELSE
        READ(LUI,REC=IREC,ERR=200) IDAY,HOUR,(H(J),THETA(J),DAYQL(J),   &
     &    DAYQVH(J),DAYSNK(J),J=1,NPT),DAYINF,DAYRAN,DAYE,DAYTRA,DAYRUN,&
     &    TPREV,TMOIST,DAYSTP,PE,PT,DAYTIM,DAYAST,DAYUBC,RHMEAN,        &
     &    TMEAN,HDRY,DAYSDH
      ENDIF
        SMOIST = TMOIST
        IF (L .EQ. 1) THEN
          WRITE(LOUT,6120) SMOIST
        ELSE
          IF ((PRESTOR-SMOIST)/SMOIST .GT. 0.0001)                      &
     &      WRITE(LOUT,6125) PRESTOR,SMOIST
        ENDIF
!
!     Read statement from UNSAT-H Version 3.00, SUMMARY subroutine
!
      READ(LUI,REC=MAXREC,ERR=200) IDAY,G,IPLANT,TPET,TPT,TTRA,TPE,TE,  &
     &  TETRAN,TRUNOF,TINF,TTIM,TRAIN,APLIED,TTIRRI,TMOIST,TERR,        &
     &  TSTP,TAST,TUBC,TRN,TSHF,TSEN,TLE,THBE,TSDH,THPREV,HSTORE,       &
     &  (TQL(I),TQVH(I),TQVT(I),TQHC(I),TSNK(I),I=1,NPT),TQHW0,         &
     &  TQHW(NPT),HSOURCE,OSMPOT
!
        WRITE(LOUT,6130) IYEAR,TRAIN,TPET,TTRA,TE,TRUNOF,TQL(NPT),      &
     &    TMOIST,TSTP,TERR
        CLOSE(UNIT=LUI)
        IYEAR      = IYEAR+1
        PRESTOR    = TMOIST
        SUM_TRAIN  = SUM_TRAIN+TRAIN
        SUM_TPET   = SUM_TPET+TPET
        SUM_TTRA   = SUM_TTRA+TTRA
        SUM_TE     = SUM_TE+TE
        SUM_TRUNOF = SUM_TRUNOF+TRUNOF
        SUM_TQL    = SUM_TQL+TQL(NPT)
        SUM_TERR   = SUM_TERR+TERR
      ENDDO
!
      IF (NF .GT. 1) THEN
        WRITE(LOUT,6095)
        WRITE(LOUT,6140) SUM_TRAIN,SUM_TPET,SUM_TTRA,SUM_TE,SUM_TRUNOF, &
     &  SUM_TQL,SUM_TERR
      ENDIF
      IF (OUT_OPT .EQ. 1) WRITE(LUS,6200) OFILE
6200  FORMAT(/,                                                         &
     &' The results were written to output file',/,1x,A79,/,            &
     &' Program BSUM completed.',//)
      CLOSE(UNIT=LOUT)
      GO TO 999
200   WRITE(LUS,6160) IREC
999   STOP ' '
!-----------------------------------------------------------------------
!     FORMAT Statements
!-----------------------------------------------------------------------
5000  FORMAT(A80)
6000  FORMAT(///,                                                       &
     &'                     Program BSUM',/,                            &
     &'                     Version ',F4.2,/,                           &
     &'             Contact:  MJ Fayer',/,                              &
     &'               email:  mike.fayer@pnl.gov',/,                    &
     &'               phone:  509-372-6045',/,                          &
     &'                 FAX:  509-372-6089',//,                         &
     &'   Program BSUM combines summary water balance information',/,    &
     &'   from multiple UNSAT-H output files (*.res) into one text',/,  &
     &'   file for easier analysis of long-term simulations',/)
6010  FORMAT(/,                                                         &
     &' BSUM Options:',/,                                               &
     &'   0) process files in the *YYYY series, where * is a global',/, &
     &'      and the YYYY is a year (e.g., 1958).  The results file',/, &
     &'      extension "*.res" is not included.  The program',/,        &
     &'      generates the remaining file names, changing only the'/,   &
     &'      year designation, e.g., *1959.res, etc.,',/,               &
     &'   1) enter the name of each file separately',/,                 &
     &'   2) end the program',//,                                       &
     &' Enter the number of your choice ==> ',$) 
6020  FORMAT(/,                                                         &
     &' Enter the name of the first file in the *YYYY series',/,        &
     &' to be summarized (e.g., for yr1958, enter yr) ==> ',$)
6030  FORMAT(/,                                                         &
     &' Enter the number of files to be summarized',                    &
     &' (a maximum of 'I3' is allowed) ==> ',$)
6035  FORMAT(/' Entering filenames one by one. Be sure to include',/,   &
     &'   filename extension (e.g., myfile.res)')
6040  FORMAT(/' Enter name of file No. 'I2' ==> ',$)
6050  FORMAT(1X,A79)
6060  FORMAT(/,                                                         &
     &' Enter (0) to accept the names or',/,                            &
     &'       (1) to re-enter the names ==> ',$)
6070  FORMAT(/,                                                         &
     &' BSUM Output Options:',/,                                        &
     &'   0) screen',/,                                                 &
     &'   1) file',/,                                                   &
     &' Enter output option ==> ',$)
6090  FORMAT(                                                           &
     &' Created using BSUM Version 'F4.2'; all units are cm',/,         &
     &' First file in series is ',A50,/,                                &
     &' Year  Precip     PET  Transp    Evap  Runoff   Drain',          &
     &'   Store  TimeStp MasBalErr')
6095  FORMAT(' ----',7(1X,7('-')),1X,8('-'),1X,9('-'))
6100  FORMAT(/,                                                         &
     &' ERROR:  BSUM Version No. ',F4.2,' can only read files',/,       &
     &'         generated from UNSAT-H Version No. ',F4.2,'.',//,       &
     &' Program stopped.'//)
6110  FORMAT(/                                                          &
     &' CAUTION: Be advised that this results file contains only',I5,/, &
     &'          records, which is'I5' less than it should have.',/)
6120  FORMAT(' Initial storage = ',T54,F8.3)
6125  FORMAT(/' WARNING: Storage values did not match between runs.',/, &
     & 1PE11.4,' cm at end of ',A30,/,                                  &
     & 1PE11.4,' cm at beginning of ',A30)
6130  FORMAT(I5,7F8.3,I9,F10.5)
6140  FORMAT(' SUM=',6F8.3,T71,F10.5)
6150  FORMAT(/' No record of file ',A50,/,' Program stopped.',/)
6160  FORMAT(/' Record No. ',I5,' could not be read',/)
9000  FORMAT(/,                                                         &
     &' Results are based on the use of unverified software.',/,        &
     &' No assurance is expressed or implied as to the accuracy,',/,    &
     &' completeness or usefulness of this information.')
!-----------------------------------------------------------------------
!     End of program BSUM
!-----------------------------------------------------------------------
      END
